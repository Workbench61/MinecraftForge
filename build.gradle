buildscript {
    repositories {
        mavenLocal()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = 'mavenCentral'
            url = 'https://repo1.maven.org/maven2/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

configurations {
    deployJars
}

import static net.minecraftforge.gradle.dev.ForgeDevPlugin.*

apply plugin: 'maven'
apply plugin: 'forgedev'

repositories {
    mavenLocal()
    maven {
        name = 'mavenCentral'
        url = 'https://repo1.maven.org/maven2/'
    }
    flatDir {
        name "fileRepo"
        dirs "repo"
    }
}

minecraft {
    version = '1.7.10'
    mcpVersion = '9.05'
    fmlDir = projectDir.getAbsolutePath() + "/fml";
    mainClass = 'cpw.mods.fml.relauncher.ServerLaunchWrapper'
    tweakClass = 'cpw.mods.fml.common.launcher.FMLTweaker'
    installerVersion = "1.4"
}

group = 'net.minecraftforge'
version = getVersionFromJava(getProject(), 'src/main/java/net/minecraftforge/common/ForgeVersion.java')

jenkins {
    job = 'minecraftforge'
}

// crowdin stuff
if (project.hasProperty("crowdinKey"))
    tasks.getLocalizations.apiKey = project.crowdinKey


launch4j {
    jreMinVersion = '1.8.0'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

def jenkBase = 'build/jenk/'
project.file(jenkBase + 'job/minecraftforge/api').mkdirs()
project.file(jenkBase + 'job/minecraftforge/api/python').createNewFile()
project.file(jenkBase + 'job/minecraftforge/lastBuild/api').mkdirs()
project.file(jenkBase + 'job/minecraftforge/lastBuild/api/python').createNewFile()
jenkins.server = 'file://' + project.file(jenkBase) + '/'

if (!System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('win')) {
    def winInstaller = project.file('build/distributions/' + archivesBaseName + '-' + version + '-installer-win.exe')
    if (!winInstaller.exists()) {
        winInstaller.parentFile.mkdirs()
        winInstaller.createNewFile()
    }
}

uploadArchives {
    repositories {
        add getProject().repositories.mavenLocal()
    }
    repositories.mavenDeployer {
        configuration = configurations.deployJars

        if (project.hasProperty("filesmaven")) {
            logger.info('Publishing to files server')
            repository(url: project.filesmaven.url) {
                authentication(userName: project.filesmaven.username, privateKey: project.filesmaven.key)
            }
        } else {
            logger.info('Publishing to repo folder')
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
        }
        pom.project {
            name project.archivesBaseName
            packaging 'jar'
            description 'Minecraft Forge API'
            url 'https://github.com/MinecraftForge/MinecraftForge'

            scm {
                url 'https://github.com/MinecraftForge/MinecraftForge'
                connection 'scm:git:git://github.com/MinecraftForge/MinecraftForge.git'
                developerConnection 'scm:git:git@github.com:MinecraftForge/MinecraftForge.git'
            }

            issueManagement {
                system 'github'
                url 'https://github.com/MinecraftForge/MinecraftForge/issues'
            }

            licenses {
                license {
                    name 'Forge Public License'
                    url 'https://raw.github.com/MinecraftForge/MinecraftForge/master/MinecraftForge-License.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'cpw'
                    name 'cpw'
                    roles { role 'developer' }
                }
                developer {
                    id 'LexManos'
                    name 'Lex Manos'
                    roles { role 'developer' }
                }
                developer {
                    id 'AbrarSyed'
                    name 'Abrar Syed'
                    roles { role 'contributor' }
                }
            }
        }
    }
}
